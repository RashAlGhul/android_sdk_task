apply plugin: 'com.android.library'
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.8.201607051106 "
}

def APP_VERSION = "2.1.1"
ext.set('AppVersion', APP_VERSION)

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"
    useLibrary 'org.apache.http.legacy'

    jacoco {
        version = "0.7.8.201607051106 "
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 3
        versionName APP_VERSION
        archivesBaseName = "mobile-connect-android-sdk"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled = true
        }
        debug {
            testCoverageEnabled = true
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
}

sonarqube {
    properties {
        property "sonar.binaries", "build/intermediates/classes/debug"
        property "sonar.java.binaries", "build/intermediates/classes/debug"
        property "sonar.java.test.binaries", "build/intermediates/classes/test/debug"
        property "sonar.sources", "src"
        property "sonar.junit.reportsPath", "build/test-results/debug"
        property "sonar.java.junit.reportsPath", "build/reports/tests/debug/index.html"
        property "sonar.android.lint.report", "build/outputs/lint-results.xml"
        property "sonar.jacoco.reportPath", "build/jacoco/testDebugUnitTest.exec"
    }
}

task jacocoTestReport(type: JacocoReport) {

    def coverageSourceDirs = [
            'src/main/java'
    ]

    group = "Reporting"
    description = "Generates Jacoco coverage reports"
    reports {
        xml{
            enabled = true
            destination "${buildDir}/reports/jacoco/jacocoTestDebugUnitTestReport/jacoco.xml"
        }
        csv.enabled false
        html{
            enabled true
            destination "${buildDir}/jacocoHtml"
        }
    }

    classDirectories = fileTree(
            dir: 'build/intermediates/classes',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/BuildConfig.*',
                       '**/Manifest*.*',
                       '**/*Activity*.*',
                       '**/*Fragment*.*'
            ]
    )

    sourceDirectories = files(coverageSourceDirs)
    additionalSourceDirs = files(coverageSourceDirs)
    executionData = files('build/outputs/code-coverage/connected/coverage.ec')
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
    compile 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    compile('com.google.guava:guava:19.0')
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0'
    compile files('libs/mobile-connect-sdk-1.0.0.jar')
}